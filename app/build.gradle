apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ae.apps.messagecounter"
        minSdkVersion rootProject.ext.minSdkVersion
        // update .travis.yml with android version
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode rootProject.ext.mainApp.appVersionCode
        versionName rootProject.ext.mainApp.appVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-DEBUG"
            minifyEnabled false
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                    'android-architecture-components.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def galaxyVersionName = variant.versionName.replaceAll('\\.', '_')
            outputFileName = "messageCounter-${variant.name}-${galaxyVersionName}-build-${variant.versionCode}.apk"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}

dependencies {
    implementation project(':core')
    //
    // Dependent Libraries
    //
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation deps.palette
    implementation deps.preference
    implementation deps.cardview
    implementation deps.supportv4
    implementation deps.aeappslib
    implementation deps.constraintlayout
    implementation deps.ankocommons
    implementation deps.osslicenses
    implementation deps.billingclient
    implementation deps.roomRuntime
    implementation deps.workRuntime
    kapt deps.roomCompiler
    //
    // Unit Testing
    //
    testImplementation deps.junit
    testImplementation deps.mockitoCore
    testImplementation deps.roboElectric
    testImplementation deps.roomTesting
    //
    // Instrumentation Testing
    //
    androidTestImplementation 'org.mockito:mockito-android:2.7.22'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation "org.hamcrest:hamcrest-core:$hamcrest_version"
    androidTestImplementation "org.hamcrest:hamcrest-integration:$hamcrest_version"
    androidTestImplementation "org.hamcrest:hamcrest-library:$hamcrest_version"
}
